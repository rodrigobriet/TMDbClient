plugins {
    id 'java'
    id 'maven-publish'
	id 'signing'
}

group 'com.github.rodrigobriet'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
	implementation('com.google.code.gson:gson:2.8.4')
	implementation('com.squareup.okhttp3:okhttp:3.10.0')
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.rodrigobriet'
            artifactId 'tmdbclient'
            version '1.0.0'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            
            // create the sign pom artifact
			pom.withXml {
			    def pomFile = file("build/generated-pom.xml")
			    writeTo(pomFile)
			    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
			    artifact(pomAscFile) {
			        classifier = null
			        extension = 'pom.asc'
			    }
			}
			
			// create the signed artifacts
			project.tasks.signArchives.signatureFiles.each {
			    artifact(it) {
			        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
			        if (matcher.find()) {
			            classifier = matcher.group(1)
			        } else {
			            classifier = null
			        }
			        extension = 'jar.asc'
			    }
			}
        }
    }
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'A java library to request content from The Movie Database API (TMDb) '
            name 'TMDb Client'
            url 'https://github.com/rodrigobriet/TMDbClient'
            organization {
                name 'com.github.rodrigobriet'
                url 'https://github.com/rodrigobriet'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/rodrigobriet/TMDbClient/issues'
            }
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://github.com/rodrigobriet/TMDbClient/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/rodrigobriet/TMDbClient'
                connection 'scm:git:git://github.com/rodrigobriet/TMDbClient.git'
                developerConnection 'scm:git:ssh://git@github.com:rodrigobriet/TMDbClient.git'
            }
            developers {
                developer {
                    name 'Rodrigo'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file('build/generated-pom.xml')
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

signing {
    sign configurations.archives
}

wrapper {
    gradleVersion = '4.7'
}